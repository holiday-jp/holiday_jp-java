// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // Project information
        version = '0.1.0'
        description = 'Japanese holiday.'
        group = 'com.github.holidayjp'
        siteUrl = 'https://github.com/holiday-jp/holiday_jp-java'
        gitUrl = 'https://github.com/holiday-jp/holiday_jp-java.git'
        userOrg = 'pinemz'

        // Library version
        findbugsVersion = '3.0.2'
        kotlinVersion = '1.2.51'
        junitVersion = '4.12'

        // Android build configuration
        compileSdkVersion = 27
        minSdkVersion = 15
        buildToolsVersion = '27.0.3'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


// Define gradle `generate` custom task
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
import com.github.holidayjp.buildscript.GeneratorTask

task generate {
    group 'HolidayJp'
    description 'Generate \'Holidays.java\' from datasheet'
    doLast {
        def rootDir = rootProject.projectDir.absolutePath
        GeneratorTask.run(rootDir)
    }
}

// Define gradle `isGitDirty` custom task
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
import com.github.holidayjp.buildscript.GitDirtyCheckerTask

task isGitDirty {
    group 'HolidayJp'
    description 'Check if git status is dirty'
    doLast {
        GitDirtyCheckerTask.run()
    }
}

// Build Time Tracker task
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~
apply plugin: 'build-time-tracker'
buildtimetracker {
    reporters {
        summary {
            ordered false
            threshold 50
            barstyle "unicode"
        }
    }
}
